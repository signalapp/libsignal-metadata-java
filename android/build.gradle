buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter {
            content {
                includeVersion 'org.jetbrains.trove4j', 'trove4j', '20160824'
            }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "signal-metadata-android"
version          = version_number
group            = group_info

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        androidTest {
            java.srcDirs = ['src/androidTest/java', project(':tests').file('src/test/java')]
        }
    }
}

repositories {
    google()
    mavenCentral()
    mavenLocal()
    jcenter {
        content {
            includeVersion 'org.jetbrains.trove4j', 'trove4j', '20160824'
        }
    }
}

dependencies {
    compile "org.whispersystems:signal-protocol-android:${protocol_version}"
    compile (project(':java')) {
        exclude group: 'org.whispersystems', module: 'signal-protocol-java'
    }
    androidTestImplementation 'junit:junit:4.12'
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//        repository(url: sonatypeRepo) {
//            authentication(userName: whisperSonatypeUsername, password: whisperSonatypePassword)
//        }
//
        pom.project {
            name 'signal-metadata-android'
            packaging 'aar'
            description 'Signal Protocol Plus cryptography library for Android'
            url 'https://github.com/SignalApp/libsignal-metadata-java'

            scm {
                url 'scm:git@github.com:SignalApp/libsignal-metadata-java.git'
                connection 'scm:git@github.com:SignalApp/libsignal-metadata-java.git'
                developerConnection 'scm:git@github.com:SignalApp/libsignal-metadata-java.git'
            }

            licenses {
                license {
                    name 'GPLv3'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Moxie Marlinspike'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}

